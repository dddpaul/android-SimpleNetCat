buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        classpath 'com.github.jcandksolutions.gradle:android-unit-test:1.2.+'
    }
}
apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 6
        versionName "1.5"
        applicationId "com.github.dddpaul.netcat"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    if(project.hasProperty("signing.properties") &&
            new File(project.property("signing.properties")).exists()) {

        Properties props = new Properties()
        props.load(new FileInputStream(file(project.property("signing.properties"))))

        signingConfigs {
            release {
                storeFile file(props["keystore"])
                keyAlias props["keyalias"]
                storePassword props["keystore.password"]
                keyPassword props["keyalias.password"]
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix '-SNAPSHOT'
        }
        release {
            signingConfig signingConfigs.release
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets {
        androidTest {
            setRoot('src/test')
        }
    }
}

apply plugin: 'android-unit-test'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:19.+'
    compile 'com.android.support:appcompat-v7:19.+'
    compile 'com.jakewharton:butterknife:5.0.+'
    compile 'de.greenrobot:eventbus:2.2.+'

    testCompile('com.squareup:fest-android:1.0.+')
    testCompile('junit:junit:4.11')
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile('org.robolectric:robolectric:2.3+') {
        exclude module: 'classworlds'
        exclude module: 'maven-artifact'
        exclude module: 'maven-artifact-manager'
        exclude module: 'maven-error-diagnostics'
        exclude module: 'maven-model'
        exclude module: 'maven-plugin-registry'
        exclude module: 'maven-profile'
        exclude module: 'maven-project'
        exclude module: 'maven-settings'
        exclude module: 'nekohtml'
        exclude module: 'plexus-container-default'
        exclude module: 'plexus-interpolation'
        exclude module: 'plexus-utils'
        exclude module: 'wagon-file'
        exclude module: 'wagon-http-lightweight'
        exclude module: 'wagon-http-shared'
        exclude module: 'wagon-provider-api'
    }
    //exclude module: 'commons-logging'
    //exclude module: 'httpclient'
}
